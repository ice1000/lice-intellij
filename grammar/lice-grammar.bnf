{
	parserClass='org.lice.lang.LiceParser'
	extends='com.intellij.extapi.psi.ASTWrapperPsiElement'
	psiClassPrefix='Lice'
	psiImplClassSuffix='Impl'
	psiPackage='org.lice.lang.psi'
	psiImplPackage='org.lice.lang.psi.impl'

	tokenTypeClass='org.lice.lang.LiceTokenType'
	elementTypeHolderClass='org.lice.lang.psi.LiceTypes'
	elementTypeClass='org.lice.lang.LiceElementType'
	psiImplUtilClass='org.lice.lang.psi.impl.LicePsiImplUtils'
}

file ::= element*
element ::= string | number | symbol | comment | null | functionCall {
	methods=[getNonCommentElements]
}

functionCall ::= LEFT_BRACKET element+ RIGHT_BRACKET {
	mixin='org.lice.lang.psi.impl.LiceFunctionCallMixin'
	implements=['org.lice.lang.psi.impl.ILiceFunctionCallMixin']
}

number ::= NUM
null ::= LEFT_BRACKET RIGHT_BRACKET
symbol ::= SYM {
	methods=[getReference getReferences]
}

string ::= STR
comment ::= LINE_COMMENT {
	methods=[isValidHost updateText createLiteralTextEscaper getTokenType]
	implements=['com.intellij.psi.PsiLanguageInjectionHost' 'com.intellij.psi.PsiComment']
}
